### LICENSE:
#
# Copyright (C) 2011 Ahmad Amarullah ( http://amarullz.com/ )
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
### FILE INFO:
#
# AROMA Installer 2.00 - AROMA Installer Test Script Configuration
#       (c) 2011-2012 by Ahmad Amarullah
#           amarullz - xda-developers
#           http://www.amarullz.com/
#
#       Binary Version  : 2.00
#       Binary Codename : Dahlia
#
### INFO:
#
# * AROMA Resource Dir  = META-INF/com/google/android/aroma
#
# * AROMA Temporary Dir = /tmp/aroma/
#
# * About Icon          = @alert - show <AROMA Resource Dir>/icons/alert.png
#                         or if theme used it will find <themedir>/icon.alert.png
#                         you can still access it via "icons/alert"
#
# * dp                  = Device Pixelate. WVGA/QHD = 3, HVGA = 2.
#                         dp calculated with = round(min(width,height) / 160)
#
###

##
# Fix Colorspace Issue
#
# For device which have a problem with color ( not display the correct color )
# try to set "force_colorspace" with ini_set. It may caused by wrong framebuffer
# information that passed by kernel into framebuffer device.
#
# You can use one of following values:
#   "rgba", "abgr", "argb", "bgra"
#
# NOTE: It must be in top most script, or you will got the color flicker
#
# ini_set("force_colorspace","rgba");
#

##
# Forcing Device Pixelate Size
#  defaut: floor( min( width, height ) / 160 )
#
# Usefull for tablet device, if interface was too big, you can
# force dp size for smaller UI size.
#
#-------- Uncomment one of this to use custom dp size:
#
# ini_set("dp","1"); #-- LDPI ( 240x320 / QVGA )
ini_set("dp","2"); #-- MDPI ( 340x480 / HVGA )
# ini_set("dp","3"); #-- HDPI ( 480x800 / WVGA )
# ini_set("dp","4");
# ini_set("dp","5");
#


##
# Calibrate Touchscreen
#  defaut: HTC Desire Touch Screen Calibrate Data [7.90,20,7.90,20]
#
# For other device, run the installer, then press "menu"
# and select Calibrating Tools. Follow Instructions, and make
# sure the touch screen has been valid in the testing step.
#
# In the end, there will be alert dialog with calibration data
# ( green ), replace this calibrate() function with that data.
#
#-------- Uncomment This to insert calibrating data:
# calibrate("7.90","20","7.90","20");
#


##
# Custom Key Code - Usefull For Some Device which not used
#                   Standard key code for up/down/select/back/menu
#
# You can capture the keycode in
# recovery using "aroma-keycapture-flashable.zip" Available in
# AROMA Installer Thread.
#
# Press some key when it run, it will return key code,
# that key can override up/down/select/back/menu actions in
# AROMA Installer, you can even set your home key for up, or
# volume down for select item
#
#-------- Uncomment & Modify this Code:
#
ini_set("customkeycode_up",     "115");
ini_set("customkeycode_down",   "114");
ini_set("customkeycode_select", "116");
ini_set("customkeycode_menu",   "229");
ini_set("customkeycode_back",   "158");
#

##
# Initializing Rom Information
#
ini_set("rom_name",             "ExMIUIv4");
ini_set("rom_version",          "2.8.24");
ini_set("rom_author",           "ExPeacer");
ini_set("rom_device",           "Xperia MDPI");
ini_set("rom_date",             "Aug, 26 2012");


##
# Show Simple Splash
#
splash(
  #-- Duration 2000ms / 2 seconds
    2000,
  
  #-- <AROMA Resource Dir>/sample.png
    "splash"
);

##
# Show Calibrating Tools Directly before show any UI
#
#--- Remove this calibtool if you already know the calibrate data
#calibtool("");

#
# Set Small Font to Support all fonts
#
fontresload( "0", "ttf/DroidSans.ttf;ttf/DroidSansArabic.ttf;ttf/DroidSansFallback.ttf;", "12" ); #-- Use sets of font (Font Family)

###################################################################################################################
#
# STEP 1 - SELECT THEME
#
##
# SET THEME
#
# Look under aroma/themes/ for theme files
#
theme("miui4");

###################################################################################################################
#
# SHOW LANGUAGE SELECTION
#
selectbox(
  #-- Title
    "Select Language",
  
  #-- Sub Title
    "Please select installer language that you want to use in this installation test:",
  
  #-- Icon: <AROMA Resource Dir>/icons/personalize.png or <ThemeDir>/icon.personalize.png
    "@default",
  
  #-- Will be saved in /tmp/aroma/theme.prop
    "lang.prop",
  
  #---------------------------------[ Selectbox Without Group ]----------------------------------#
  # TITLE            |  SUBTITLE                                                 | Initial Value #
  #------------------+-----------------------------------------------------------+---------------#

    "English",            "Welcome to Installer",                                        1,      #-- selected.0 = 1
	"Hungarian",            "Üdvözlet",									 0 		 #-- selected.0 = 2
    #"Indonesian",         "Selamat datang di Installer",                                 0,      #-- selected.0 = 3
    #"Espanol",            "Bienvenido al Instalador",                                    0,      #-- selected.0 = 4
    #"Simplified Chinesse","欢迎到安装",                                                    0,      #-- selected.0 = 5
    #"Arabic",             "مرحبا بكم في المثبت",                                               0,      #-- selected.0 = 6       
    #"French",             "Bienvenue dans l'installateur",                               0,      #-- selected.0 = 7
    #"Russian",            "Добро пожаловать в установщик",                               0,      #-- selected.0 = 8
	#"Italian",            "Benvenuti Installer",                                         0,      #-- selected.0 = 9
	#"Hebrew",             "ברוכים הבאים להתקנה",										     0,		 #-- selected.0 = 10
	#"Germany",            "Willkommen bei Installer",									 0 		 #-- selected.0 = 11
  #--------[ Initial Value = 0: Unselected, 1: Selected, 2: Group Item, 3: Not Visible ]---------#
);

##
# SET LANGUAGE & FONT FAMILY
#
if prop("lang.prop","selected.0")=="1" then
  loadlang("langs/en.lang");
  fontresload( "0", "ttf/DroidSans.ttf", "12" ); #-- "0" = Small Font ( Look at Fonts & UNICODE Demo Below )
  fontresload( "1", "ttf/DroidSans.ttf", "18" ); #-- "1" = Big Font
endif;

if prop("lang.prop","selected.0")=="2" then
  loadlang("langs/hu.lang");
  fontresload( "0", "ttf/DroidSans.ttf", "12" );
  fontresload( "1", "ttf/DroidSans.ttf", "18" );
endif;


if prop("lang.prop","selected.0")=="3" then
  loadlang("langs/id.lang");
  fontresload( "0", "ttf/DroidSans.ttf", "12" );
  fontresload( "1", "ttf/DroidSans.ttf", "18" );
endif;

if prop("lang.prop","selected.0")=="4" then
  loadlang("langs/es.lang");
  fontresload( "0", "ttf/DroidSans.ttf", "12" );
  fontresload( "1", "ttf/DroidSans.ttf", "18" );
endif;

if prop("lang.prop","selected.0")=="5" then
  loadlang("langs/cn.lang");
  fontresload( "0", "ttf/DroidSansFallback.ttf;ttf/DroidSans.ttf", "12" ); #-- Use sets of font (Font Family)
  fontresload( "1", "ttf/DroidSansFallback.ttf;ttf/DroidSans.ttf", "18" );
endif;

if prop("lang.prop","selected.0")=="6" then
  loadlang("langs/ar.lang");
  fontresload( "0", "ttf/DroidSansArabic.ttf;ttf/DroidSans.ttf", "12" ); #-- Use sets of font (Font Family)
  fontresload( "1", "ttf/DroidSansArabic.ttf;ttf/DroidSans.ttf", "18" );
endif;

if prop("lang.prop","selected.0")=="7" then
  loadlang("langs/fr.lang");
  fontresload( "0", "ttf/DroidSans.ttf", "12" );
  fontresload( "1", "ttf/DroidSans.ttf", "18" );
endif;

if prop("lang.prop","selected.0")=="8" then
  loadlang("langs/ru.lang");
  fontresload( "0", "ttf/DroidSans.ttf", "12" );
  fontresload( "1", "ttf/DroidSans.ttf", "18" );
endif;

if prop("lang.prop","selected.0")=="9" then
  loadlang("langs/it.lang");
  fontresload( "0", "ttf/DroidSans.ttf", "12" );
  fontresload( "1", "ttf/DroidSans.ttf", "18" );
endif;

if prop("lang.prop","selected.0")=="10" then
  loadlang("langs/he.lang");
  fontresload( "0", "ttf/DroidSans.ttf", "12" );
  fontresload( "1", "ttf/DroidSans.ttf", "18" );
endif;

if prop("lang.prop","selected.0")=="11" then
  loadlang("langs/de.lang");
  fontresload( "0", "ttf/DroidSans.ttf", "12" );
  fontresload( "1", "ttf/DroidSans.ttf", "18" );
endif;


###################################################################################################################
#
# STEP 2 - SHOW ROM INFORMATION
#
viewbox(
  #-- Title
    "<~welcome.title>",
  
  #-- Text
    "<~welcome.text1> <b>"+
	  #-- Get Config Value
	  ini_get("rom_name")+
	"</b> <b>"+ini_get("rom_device")+"</b> <~common.for>.\n\n"+
    
    "<~welcome.text2>\n\n"+
	
      "  <~welcome.version>\t: <b><#selectbg_g>"+ini_get("rom_version")+"</#></b>\n"+
      "  <~welcome.updated>\t: <b><#selectbg_g>"+ini_get("rom_date")+"</#></b>\n\n\n"+

    "<~welcome.next>",
  
  #-- Icon
    "@welcome"
);


###################################################################################################################
#
# STEP 3 - LICENSE
#
agreebox(
  #-- Title
    "<~terms.title>",
  
  #-- Subtitle / Description
    "<~terms.desc>",
  
  #-- Icon
    "@license",
  
  #-- Text Content ( Read from <AROMA Resource Dir>/license.txt )
    resread("license.txt"),
  
  #-- Checkbox Text
    "<~terms.check>",
  
  #-- Unchecked Alert Message
    "<~terms.confirm>"
);

selectbox(
  # Title
    "<~devselect.title>",
  # Sub Title
    "",
  # Icon
    "icons/install",
  # Will be saved in /tmp/aroma-data/<file>
    "device.prop",
  # Items ( per 3 arguments ): Title, Subtitle/description, Icon
    "Mini",      "ST15i - Smultron", "",
    "Mini Pro", "SK17i - Mango", "",
    "Live With Walkman", "WT19i - Coconut", "",
    "Active", "ST17i - Satsuma", ""
); 

selectbox(
  # Title
    "<~bootloader.title>",
  # Sub Title
    "",
  # Icon
    "icons/install",
  # Will be saved in /tmp/aroma-data/<file>
    "bl.prop",
  # Items ( per 3 arguments ): Title, Subtitle/description, Icon
    # Item 1
    "Locked", "Select that if you have LOCKED bootloader", "",
    # Item 2
    "Unlocked", "Select this if you have UNLOCKED bootloader", ""

); 



##
#
# Next Screen is Installation UI, So change the next button text to "Install Now"
#
ini_set("text_next", "<~installnow>");

###################################################################################################################
#
# STEP 9 - MAKE SURE USER KNOW THAT NEXT STEP WAS INSTALLATION PROCESS
#
viewbox(
  #-- Title
    "<~install.ready>",

  #-- Text
    "<~install.readytobegin>.\n\n"+
	"<~install.now>.\n\n",

  #-- Icon
    "@install"
);

##
#
# Now back to English
#
loadlang("langs/en.lang");
fontresload( "0", "ttf/DroidSans.ttf", "12" );
fontresload( "1", "ttf/DroidSans.ttf", "18" );

##
#
# Next Button in successfull installation should be back to "Next >"
#
ini_set("text_next", "Next >");

###################################################################################################################
#
# STEP 10 - INSTALLATION PROCESS
#
setvar("retstatus",
  install(
	#-- Title
      "Installing",
	
	#-- Installation Process message
	  "<b>"+ini_get("rom_name")+"</b> are being installed.\n\n"+
      "Please wait while installation wizard installs <b>"+ini_get("rom_name")+
	  "</b>. This may take several minutes.",
	
	#-- Installation Icon
      "@install",
	
	#-- Installation Finish Message
      "The installation wizard has successfully installed <b>"+ini_get("rom_name")+
	  "</b>. Press Next to continue."
  )
);

##
#
# Next Button is "Finish"
#
ini_set("text_next", "Finish");

###################################################################################################################
#
# STEP 11 - FINISH MESSAGE, Add Checkbox to Reboot or No. Save in "reboot_it" variable
#
# You can use return value or "reboot_it" variable to retrive checkbox value
# Example:
# if (checkviewbox(....)=="1" then
#    ... checked ...
# endif;
#
checkviewbox(
  #-- Title
    "Installation Completed",
  
  #-- Text
    "<#selectbg_g><b>Congratulation...</b></#>\n\n"+
    "<b>"+ini_get("rom_name")+"</b> has been installed into your device.\n\n"+
    "Installer Status: "+getvar("retstatus")+"\n\n",
	
  #-- Icon
    "@welcome",

  #-- Checkbox Text
    "Reboot your device now.",

  #-- Initial Checkbox value ( 0=unchecked, 1=checked ) -  (Optional, default:0)
    "1",

  #-- Save checked value in variable "reboot_it" (Optional)
    "reboot_it"
);

###
#
# Check if reboot checkbox was checked
if
  getvar("reboot_it")=="1"
then
  #
  # reboot("onfinish");   - Reboot if anything finished
  # reboot("now");        - Reboot Directly
  # reboot("disable");    - If you set reboot("onfinish") before, use this command to revert it.
  #
  reboot("onfinish");
endif;

#---- FINISH
